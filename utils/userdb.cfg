defaultvalue "user_db_name" "default"
defaultvalue "table_user" "users"
defaultvalue "table_pass" "users_passwords"
defaultvalue "table_groups" "users_roles"

user_db = (db_get_dbuid $user_db_name)
tags = (ListTags)

FinalizeQueries = [
//@function FinalizeQueries intlist queries
looplist query $arg1 [
        (mysql_finalize $user_db $query)
]
]

EscapeResult = [
//@function EscapeResult string result
//@return string escapedresult
        result (substr $arg1 1 (- (strlen $arg1) 2))
]

UserExists = [
//@function UserExists string name
//@return int uid

quid = (mysql_pquery $user_db "SELECT uid FROM :0 WHERE name=':1'" $table_user $arg1)

//echo (format "SELECT uid FROM %1 WHERE name='%2'" $table_user $arg1)
if (= $quid -1) [
        (mysql_error $user_db)
        result -1
] [
        uid = (mysql_getrow $quid)
        (FinalizeQueries (concat $quid $uid))
        result (EscapeResult $uid)
]
]

UserPass = [
//@function UserPass int uid
//@return string hash

qpass = (mysql_pquery $user_db "SELECT field_server_password_value FROM :0 WHERE entity_id=':1'" $table_pass $arg1)

if (= $qpass -1) [
        (mysql_error $user_db)
        result -1
] [
        pass = (mysql_getrow $qpass)
        (FinalizeQueries (concat $qpass $pass))
        result (EscapeResult $pass)
]
]

GetUser = [
//@function GetUser int uid string name
//@return array user

groups = (UserGroups $arg1)
gid = (min (at $groups (indexof $groups $gid_mod)) (at $groups (indexof $groups $gid_adm)))

user = (format "%1 %2 %3" $arg1 $arg2 $gid)

result $user
]

UserCanLogin = [
//@function UserCanLogin int cn
//@return bool canlogin

cuid = (UserExists (getname $arg1))

res = ""

if (> $cuid -1) [
	if (= $cuid (getEntryDetail $arg1 "uid")) [
		res = 1
	] [
		res = 0
	]
] [
	res = 1
]

result $res
]

UserGroups = [
//@function UserGroup int uid
//@return intlist groups

qgroup = (mysql_pquery $user_db "SELECT rid FROM :0 WHERE uid=':1'" $table_groups $arg1)

if (= $qgroup -1) [
        (mysql_error $user_db)
        result -1
] [
        groups = ""
        while [row = (mysql_getrow $qgroup); result (!=s $row "")] [
                groups = (concat $groups (EscapeResult $row))
        ]
        (FinalizeQueries (concat $qgroup $row))
        result $groups
]
]

UserTags = [
//@function UserTags int uid
//@return stringlist tid

qtags = (mysql_pquery $user_db "SELECT tid FROM :0 WHERE uid=':1'" $table_tags_member $arg1)
if (= $qtags -1) [
        (mysql_error $user_db)
        result -1
] [
        tags = ""
        while [row = (mysql_getrow $qtags); result (!=s $row "")] [
                tags = (concat $tags (EscapeResult $row))
        ]
        (FinalizeQueries $qtags)
        result $tags
]
]

TagToTid = [
//@function TagToId string tag
//@return int tid

qtid = (mysql_pquery $user_db "SELECT tid FROM :0 WHERE name=':1'" $table_tags $arg1)

if (= $qtid -1) [
        (mysql_error $user_db)
        result -1
] [
        tid = (mysql_getrow)
        (FinalizeQueries (concat $qtid $tid))
        result (EscapeResult $tid)
]
]

ListTags = [
//@function ListTags
//@return stringlist name

qtags = (mysql_pquery $user_db "SELECT name FROM :0" $table_tags)
if (= $qtags -1) [
        (mysql_error $user_db)
        res = -1
] [
        tags = ""
        while [row = (mysql_getrow $qtags); result (!=s $row "")] [
                tags = (concat $tags (EscapeResult $row))
        ]
        (FinalizeQueries (concat $qtags $row))
        result $tags
]
]
